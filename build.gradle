buildscript {
    repositories { 
        jcenter() 
    }
    dependencies {
        classpath 'com.tunnelvisionlabs:antlr4:4.4'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.+'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'license'

if ( file( 'deploy.gradle' ).exists() ) {
    apply from: 'deploy.gradle'
}

version = "2.3.0-beta"
group = "net.mobtalker.mobtalkerscript"
archivesBaseName = "MobTalkerScript"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.tunnelvisionlabs:antlr4-runtime:4.4'
    compile 'org.apache.commons:commons-lang3:3.3.+'
    compile 'com.google.guava:guava:17.+'
    compile 'net.sf.jopt-simple:jopt-simple:4.5'
    
    testCompile 'junit:junit:4.+'
}

jar {
    from 'LICENSE'
}

shadowJar {
    from 'LICENSE'
    exclude 'META-INF', 'META-INF/**'

    manifest {
        attributes 'Main-Class': 'net.mobtalker.mobtalkerscript.standalone.MobTalkerScript'
    }
    
    classifier = 'standalone'
}

task sourceJar( type: Jar ) {
    from sourceSets.main.allSource
    classifier = 'src'
}

artifacts {
    archives shadowJar 
    archives sourceJar
}

task generateAntlr( type: JavaExec ) {
    def grammarFile = 'src/main/antlr/Mts.g4'
    def outputDir = 'src/main/java/net/mobtalker/mobtalkerscript/v2/compiler/antlr/'

    main 'org.antlr.v4.Tool'
    classpath buildscript.configurations.classpath
    
    inputs.file grammarFile
    outputs.files(
        "${outputDir}/MtsParser.java",
        "${outputDir}/MtsLexer.java",
        "${outputDir}/MtsVisitor.java",
        "${outputDir}/MtsBaseVisitor.java",
        "${outputDir}/Mts.tokens",
        "${outputDir}/MtsLexer.tokens"
    )
    
    args = [ 
        grammarFile,
        '-o', outputDir,
        '-package', 'net.mobtalker.mobtalkerscript.v2.compiler.antlr',
        '-visitor',
        '-no-listener',
        '-encoding', 'UTF-8'
    ]
}
tasks.compileJava.dependsOn generateAntlr

task cleanAntlr( type: Delete ) {
    delete generateAntlr.outputs.getFiles()
}

license {    
    include "**/*.java"
    excludes ([
        "**/MtsParser.java", 
        "**/MtsLexer.java", 
        "**/MtsVisitor.java", 
        "**/MtsBaseVisitor.java"
    ])
    
    header project.file( 'HEADER' )    
    
    ignoreFailures false
    strictCheck false
 
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
