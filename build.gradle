buildscript {
    repositories { 
        jcenter() 
    }
    dependencies {
        classpath 'com.tunnelvisionlabs:antlr4:4.4'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.+'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'license'

if ( file( 'deploy.gradle' ).exists() ) {
    apply from: 'deploy.gradle'
}

group = "net.mobtalker"
archivesBaseName = "MobTalkerScript"
version = "3.0.0-beta"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.tunnelvisionlabs:antlr4-runtime:4.4'
    compile 'org.apache.commons:commons-lang3:3.3.+'
    compile 'com.google.guava:guava:17.+'
    compile 'net.sf.jopt-simple:jopt-simple:4.5'
    
    testCompile 'junit:junit:4.+'
}

jar {
    from 'LICENSE-MobTalkerScript', 'LICENSE-GPL', 'LICENSE-LGPL'
}

shadowJar {
    from 'LICENSE-MobTalkerScript', 'LICENSE-GPL', 'LICENSE-LGPL'
    exclude 'META-INF', 'META-INF/**'

    manifest {
        attributes 'Main-Class': 'net.mobtalker.mobtalkerscript.standalone.MobTalkerScript'
    }
    
    classifier = 'standalone'
}

task sourceJar( type: Jar ) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives shadowJar 
    archives sourceJar
}

task generateAntlr( type: JavaExec ) {
    def grammarFile = 'src/main/antlr/Mts3.g4'
    def outputDir = 'src/main/java/net/mobtalker/mobtalkerscript/v3/compiler/antlr/generated'

    main 'org.antlr.v4.Tool'
    classpath 'src/main/antlr/templates'
    classpath buildscript.configurations.classpath
    
    inputs.file grammarFile
    inputs.file 'src/main/antlr/templates/org/antlr/v4/tool/templates/codegen/Java/Java.stg'
    //outputs.dir( "${outputDir}" )
    
    args = [ 
        grammarFile,
        '-o', outputDir,
        '-package', 'net.mobtalker.mobtalkerscript.v3.compiler.antlr.generated',
        '-no-visitor',
        '-listener',
        //'-no-listener',
        '-encoding', 'UTF-8',
        //'-Xlog',
        //'-XdbgST'
    ]
}
tasks.compileJava.dependsOn generateAntlr

task cleanAntlr( type: Delete ) {
    delete generateAntlr.outputs.getFiles()
}

license {    
    include "**/*.java"
    exclude "**/generated/**"
    
    header project.file( 'HEADER' )    
    
    ignoreFailures false
    strictCheck false
 
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
