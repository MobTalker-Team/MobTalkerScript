// SPDX-FileCopyrightText: 2013-2020 Chimaine, MobTalkerScript contributors
//
// SPDX-License-Identifier: LGPL-3.0-or-later

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.tunnelvisionlabs:antlr4:4.4'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.+'
        classpath 'com.github.johnrengelman:shadow:8.+'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.+'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'license'

ext {
    mtsRevision = '3.0.6'
    mtsStatus = 'beta'
    mtsVersion = mtsRevision + '-' + mtsStatus
}

group = "net.mobtalker"
archivesBaseName = "MobTalkerScript"
version = mtsVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'com.tunnelvisionlabs:antlr4-runtime:4.+'
    implementation 'com.tunnelvisionlabs:antlr4-runtime:4.+'
    implementation 'org.apache.commons:commons-lang3:3.3.+'
    implementation 'net.sf.jopt-simple:jopt-simple:4.5'
    
    testImplementation 'junit:junit:4.+'
}

jar {
    from 'LICENSE-MobTalkerScript', 'LICENSE-GPL', 'LICENSE-LGPL'
}

shadowJar {
    from 'LICENSE-MobTalkerScript', 'LICENSE-GPL', 'LICENSE-LGPL'
    exclude 'META-INF', 'META-INF/**'

    manifest {
        attributes 'Main-Class': 'net.mobtalker.mobtalkerscript.standalone.MobTalkerScript'
    }
    
    archiveClassifier.set('standalone')
}

tasks.register('sourceJar', Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives shadowJar 
    archives sourceJar
}

tasks.register('generateAntlr', JavaExec) {
    def grammarFile = 'src/main/antlr/Mts3.g4'
    def outputDir = 'src/main/java/net/mobtalker/mobtalkerscript/v3/compiler/antlr/generated'

    it.main 'org.antlr.v4.Tool'
    it.classpath 'src/main/antlr/templates'
    it.classpath buildscript.configurations.classpath

    it.inputs.file grammarFile
    it.inputs.file 'src/main/antlr/templates/org/antlr/v4/tool/templates/codegen/Java/Java.stg'
    //it.outputs.dir( "${outputDir}" )

    it.args = [
            grammarFile,
            '-o', outputDir,
            '-package', 'net.mobtalker.mobtalkerscript.v3.compiler.antlr.generated',
            '-no-visitor',
            '-listener',
            //'-no-listener',
            '-encoding', 'UTF-8',
            //'-Xlog',
            //'-XdbgST'
    ]

    // Dirty fix for antlr/antlr4#753 in Antlr 4.4
    // Taken and adapted from here:
    // https://github.com/apache/groovy/blob/master/subprojects/parser-antlr4/build.gradle#L34
    it.doLast {
        def parserFilePattern = 'Mts3*'
        copy {
            from "$outputDir/src/main/antlr"
            into outputDir
            include parserFilePattern
        }
        delete "$outputDir/src"
    }
}
tasks.compileJava.dependsOn generateAntlr

tasks.register('cleanAntlr', Delete) {
    it.delete generateAntlr.outputs.getFiles()
}

license {    
    include "**/*.java"
    exclude "**/generated/**"
    
    header project.file( 'HEADER' )
    
    ignoreFailures false
    strictCheck false
}

tasks.withType(JavaCompile).configureEach {
    it.options.compilerArgs << '-Xlint:deprecation'
}

if ( file( 'deploy.gradle' ).exists() ) {
    apply from: 'deploy.gradle'
}